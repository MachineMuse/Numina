buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

plugins {
    id "com.github.johnrengelman.shadow" version "4.0.4"
    id 'idea'
}
apply plugin: 'net.minecraftforge.gradle.forge'

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

def buildnumber = System.getenv("BUILD_NUMBER")
def travisbuildnumber = (System.getenv("TRAVIS_BUILD_NUMBER") ?: -000).toInteger() + 00
version = "${minecraft_version}-${mod_version}.${buildnumber ?: travisbuildnumber}"
group = group_id // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = mod_id

repositories {
    //url                                                           //dependencies
    maven { url = "http://maven.ic2.player.to" }                    //IC2 Forestry
    maven { url = 'http://maven.mcmoddev.com' }                     //TESLA API
    maven { url = "http://maven.covers1624.net/" }                  //CoFH

}




dependencies {
    compile group: 'org.apache.commons', name: 'commons-text', version: '1.6'  // Apache commons-text used for word wrapping
    shadow group: 'org.apache.commons', name: 'commons-text', version: '1.6'  // Apache commons-text used for word wrapping (Shadow copy)
    deobfCompile "net.sengir.forestry:forestry_${minecraft_version}:${forestry_version}"            //Forestry
    compile "net.darkhax.tesla:Tesla-${minecraft_version}:${tesla_version}"                         //Tesla
    deobfCompile "cofh:RedstoneFlux:1.12-${rf_version}:universal"
    deobfCompile "net.industrial-craft:industrialcraft-2:${ic2api_version}-ex112"                   //IC2 API
    
}

shadowJar {
    archiveName = tasks.jar.archiveName
    classifier = ""
    exclude 'org.apache.commons.lang*'
    relocate 'org.apache.commons.text', "net.machinemuse.repack.org.apache.commons.text"
    configurations = [project.configurations.shadow]
}

reobf {
    shadowJar {
                mappingType = "SEARGE"
            }
}

tasks.reobfShadowJar.mustRunAfter shadowJar

minecraft {
    version = minecraft_version + "-" + forge_version// + "-" + "1.10.0"
    runDir = "run"
    mappings = mcp_mappings
    replace "@VERSION@", project.version
    useDepAts = true
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
    // Move access transformers to META-INF
    rename '(.+_at.cfg)', 'META-INF/$1'
}

jar {
    manifest {
        attributes 'FMLAT': 'numina_at.cfg'
    }
}

idea {
    module {
        inheritOutputDirs = true
    }
}

runClient {
    jvmArgs "-Xmx3G"
}
